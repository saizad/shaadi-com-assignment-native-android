plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

//Common
def PACKAGE_NAME = "com.shaadi.assignment"
def VERSION_CODE = 1

def VERSION_NAME = "0.0.1"

//local
def DOMAIN_URL_LOCAL = "http://192.168.0.112:8000/"

//Dev
def DOMAIN_URL_DEV = "https://reqres.in/"

//Stg
def DOMAIN_URL_STG = "http://167.71.236.237:8080/"

//Prod
def DOMAIN_URL_PROD = "http://167.71.236.237:8080/"

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 23
        targetSdkVersion 33
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "com.shaadi.assignment.CustomTestRunner"
        multiDexEnabled true
        archivesBaseName = "$versionName(v$versionCode)"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            versionNameSuffix '-debug'
            applicationIdSuffix ".debug"
        }

        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors{

        dev {
            versionName VERSION_NAME + "-dev"
            buildConfigField "String", "VERSION_CODE_NAME", "\"${VERSION_NAME}\""
            buildConfigField "String", "DOMAIN_URL", "\"${DOMAIN_URL_DEV}\""
            applicationIdSuffix ".dev"
            manifestPlaceholders = [appRoundIcon: "@mipmap/ic_dev_launcher_round", appIcon: "@mipmap/ic_dev_launcher", appName: "[Dev]VmFrameworkExample"]
            buildConfigField "String", "TEST_USERNAME", "\"VmFrameworkExample@VmFrameworkExample.com\""
            buildConfigField "String", "TEST_PASSWORD", "\"testuser\""
        }

        prod {
            versionName VERSION_NAME + "-prod"
            buildConfigField "String", "VERSION_CODE_NAME", "\"${VERSION_NAME}\""
            buildConfigField "String", "DOMAIN_URL", "\"${DOMAIN_URL_PROD}\""
            manifestPlaceholders = [appRoundIcon: "@mipmap/ic_launcher_round", appIcon: "@mipmap/ic_launcher", appName: "VmFrameworkExample"]
            buildConfigField "String", "TEST_USERNAME", "\"\""
            buildConfigField "String", "TEST_PASSWORD", "\"\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
        }
    }

    buildFeatures {
        viewBinding = true
    }

    android.defaultConfig.vectorDrawables.useSupportLibrary = true

}

kapt {
    javacOptions {
        option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.intuit.ssp:ssp-android:1.0.6'
    implementation 'com.intuit.sdp:sdp-android:1.0.6'

    implementation ('com.github.saizad:vm-framework:9962c0f0032d2532dbdc75f364ae0830b871e172') {
        exclude module: "play-services-ads-identifier"
        exclude module: "play-services-measurement"
        exclude module: "play-services-measurement-sdk"
    }

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"

    implementation("androidx.room:room-ktx:$room_version")
    implementation("androidx.room:room-runtime:$room_version")
    kapt "androidx.room:room-compiler:$room_version"

}
